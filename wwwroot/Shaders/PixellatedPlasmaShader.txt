precision highp float; 
uniform float time; 
uniform vec2 resolution; 
uniform vec2 mouse; 

// sin() that returns 0-1
float nsin(float a)
{
    return (sin(a)+1.)/2.;
}
float ncos(float a)
{
    return (cos(a)+1.)/2.;
}

// return 0-1
float plasma_a(vec2 uv, float t, vec2 lbound, vec2 ubound)
{
    vec2 p1 = vec2(nsin(t * 1.3), nsin(t * 1.9));
    vec2 p2 = vec2(nsin(t * 1.2), nsin(t * 2.2));
    p1 = (p1 * (ubound - lbound)) + lbound;
    p2 = (p2 * (ubound - lbound)) + lbound;

    return
        (nsin(length(p1 - uv))
        + nsin(length(p2 - uv))
        + nsin(uv.x / 3.)
        + nsin(uv.y / 2.)
        ) / 4.
        ;
}

// like smootherstep, but returns 0.0 at both edges, and 1.0 in the center (instead of a ramp, it's a hill)
float tri_step(float lbound, float ubound, float val)
{
    float halfRange = (ubound - lbound) / 2.0;
    val -= lbound;// shift down to 0.0 - range
    val -= halfRange;// shift down to -halfrange - halfrange
    val = abs(val);// make inverted triangle
    val = halfRange - val;// invert it so it's the kind of triangle we want (0 at the ends)
	val = val / halfRange;// scale triangle to 0-1
    val = clamp(val, 0.0, 1.0);
    return val;
}


// convert a 1D value to color, mixing channels
vec3 a_to_color(float a)
{
    return vec3(
        tri_step(0.,0.75, 1.-a),
        tri_step(0.12,0.95, 1.-a),
        tri_step(0.4,1.0, 1.-a)
    );
}


vec2 getuv_centerX(vec2 fragCoord, vec2 newTL, vec2 newSize, out float vignetteAmt)
{
    vec2 ret = vec2(fragCoord.x / resolution.x, (resolution.y - fragCoord.y) / resolution.y);// ret is now 0-1 in both dimensions
    
    // vignette. return 0-1
    vec2 vignetteCenter = vec2(0.5, 0.5);// only makes sense 0-1 values here
    //if(mouse.z > 0.)
    //    vignetteCenter = vec2(mouse.x / resolution.x, (resolution.y - mouse.y) / resolution.y);// ret is now 0-1 in both dimensions;
	vignetteAmt = 1.0 - distance(ret, vignetteCenter);
//    vignetteAmt = pow(vignetteAmt, 1.);
    
    ret *= newSize;// scale up to new dimensions
    float aspect = resolution.x / resolution.y;
    ret.x *= aspect;// orig aspect ratio
    float newWidth = newSize.x * aspect;
    return ret + vec2(newTL.x - (newWidth - newSize.x) / 2.0, newTL.y);
}



void main()
{
	float tempTime = time*3.0;
    vec2 lbound = vec2(0., 0.);
    vec2 ubound = vec2(10., 10.);
    float vignetteAmt;
	vec2 uv = getuv_centerX(gl_FragCoord.xy, vec2(0.), vec2(10.), vignetteAmt);

    // background
    gl_FragColor = vec4(1.,1.,1.,1.);

    // pixellate effect
    //const float pixelCount = 18.;
    float pixelSize = 44./resolution.x * ubound.x;// pixels wide always.
        
    if(mouse.x > 0.)
        pixelSize = (mouse.x+12.)/resolution.x * ubound.x;// pixels wide always.
    
    vec2 plasma_uv = floor((uv / pixelSize) + 0.5) * pixelSize;// pixellated uv coords

    // plasma
    float a = plasma_a(plasma_uv, tempTime, lbound, ubound);
    gl_FragColor = vec4(a_to_color(a), 1.0);
    
    // distance to pixel center
    const float pi2 = 3.14159 * 2.0;
    float pixelBorderFX = ncos(uv.x / pixelSize * pi2);
    pixelBorderFX = min(pixelBorderFX, ncos(uv.y / pixelSize * pi2));
    pixelBorderFX = pow(pixelBorderFX, 0.1);
    gl_FragColor.rgb *= pixelBorderFX;
    
    // apply vignette
    gl_FragColor.rgb *= ((vignetteAmt + 0.1) * 2.) - 0.3;
    gl_FragColor.rgb = clamp(gl_FragColor.rgb, 0.,1.);
}


